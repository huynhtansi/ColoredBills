apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def gitCommitCount = 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {

    signingConfigs {
        release_key {
            storeFile file("../keystore/coloredbills_release_keystore.jks")
            keyAlias 'COLOREDBILLS_RELEASE_KEYSTORE'
            keyPassword 'htsi.release'
            storePassword 'htsi.release'
        }
        debug_key {
            storeFile file("../keystore/coloredbills_debug_keystore.jks")
            keyAlias 'COLOREDBILLS_DEBUG_KEYSTORE'
            keyPassword 'htsi.dev'
            storePassword 'htsi.dev'
        }
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "htsi.com.coloredbills"
        minSdkVersion 21
        targetSdkVersion 24

        archivesBaseName = "ColoredBills"
        versionName "1.0"
        versionCode gitCommitCount
        signingConfig signingConfigs.debug_key
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_key
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug_key
        }
    }

    applicationVariants.all { variant ->
        final def each = variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.
                    replace(".apk", "_" + versionName + "_" + versionCode + "_" + getDate() + ".apk"));
        }
        each
    }

}

ext {
    // Android Support AppCompat
    supportLibVersion = '24.2.1'
    // Dagger
    daggerLibVersion = '2.7'
    // Butter Knife
    butterknifeLibVersion = '8.1.0'
    // Fonts Support
    calligraphyLibVersion = '2.2.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    // Android Support AppCompat
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    //
    compile "com.google.dagger:dagger:${daggerLibVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerLibVersion}"
    // Butter Knife
    compile "com.jakewharton:butterknife:${butterknifeLibVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterknifeLibVersion}"
    // Fonts Support
    compile "uk.co.chrisjenx:calligraphy:${calligraphyLibVersion}"

}
